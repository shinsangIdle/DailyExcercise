result-view{
	match : Exercise(this)
	
	
	message{
		template("오늘 해야할 운동이에요."){
			speech ("오늘 남은 운동 목록이에요.  먼저 시작할 운동을 선택해주세요")
		}
	}
	
	render{
		if (size(this)>=1){
			layout{
				section{
					content{
						for-each (this){
							as (result){
								thumbnail-card{
									title-area{
										slot1{
											text{
												style(Title_S)
												value("#{value(result.exerciseName)}")
											}
										}
										slot2{
											paragraph{
												style(Detail_M_Soft)
												value("#{value(result.exerciseCnt)}회 #{value(result.exerciseSet)}세트")
											}
										}
									}
									image-url ("#{value(result.exerciseImgUrl)}")
									image-position (Start)
									on-click {
										intent {
											goal: PlayCount
											value: $expr(result.exerciseName)
										}
									}
								}
							}
						}
					}
				}
			}
		}else{
			layout{
				section{
					title("아직 오늘의 루틴을 설정 하지 않았어요.")
				}
			}
			
		}
	}
	conversation-drivers{
		if (size(this)>=1){
			conversation-driver{
				template ("#{value(this[0].exerciseName)} 시작할게")
			}
			conversation-driver{
				template ("#{value(this[0].exerciseName)} 카운트 해줘")
			}
		}
		else{
			conversation-driver{
				template ("상체 운동으로 추천해줘")
			}
			conversation-driver{
				template ("오늘 해야 될 운동 알려줘")
			}
		}
	}
}